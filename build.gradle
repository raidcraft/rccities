buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'kr.entree.spigradle' version '2.2.3'
    id 'io.ebean' version '12.9.1'
    id 'io.freefair.lombok' version '5.3.3.3'
    id 'java'
    id 'jacoco'
    id 'idea'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 11
targetCompatibility = 11

ext {
    mcVersion = project.property("mcVersion")
}

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    depends = ['WorldGuard', 'WorldEdit', 'ProtocolLib', 'Vault', 'ebean-wrapper', 'dynmap']
    softDepends = ['art-framework', 'RCEconomy']
}

archivesBaseName = project.property("pluginName")

group project.property("group")

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url = 'https://jitpack.io' }
    spigot()
    enginehub()
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = uri("https://mvn.intellectualsites.com/content/repositories/releases/") } // FAWE
}

dependencies {
    implementation spigot(mcVersion)

    implementation 'de.raidcraft.economy:wrapper:2.4.0'
    implementation 'net.silthus:ebean-wrapper:2.9.1'
    implementation 'net.silthus.configlib:configlib-bukkit:2.6.0'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.3'

    implementation group: 'io.art-framework', name: 'art-core', version: '4.5.4'

    implementation 'com.sk89q.worldguard:worldguard-bukkit:7.0.5'
    implementation "com.intellectualsites.fawe:FAWE-Bukkit:1.16-687"
    implementation "com.intellectualsites.fawe:FAWE-Core:1.16-687"
    implementation 'com.github.MilkBowl:VaultAPI:1.7'

    implementation 'com.github.webbukkit.dynmap:Dynmap:3.1'
    implementation 'com.github.webbukkit:dynmap-api:2.5'

    testImplementation 'io.ebean:ebean-test:12.9.1'

    testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    testImplementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.3'
    testImplementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.25'
    testImplementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.34.0'
    testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.2.21'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.assertj:assertj-core:3.+'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.16:1.1.0'
}

configurations {
    all*.exclude group: 'it.unimi.dsi'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('co.aikar:acf-paper:'))
        include(dependency('net.silthus.configlib:configlib-bukkit:'))
        include(dependency('com.fasterxml.jackson.core::'))
        include(dependency('de.raidcraft.economy:wrapper:'))
    }
    relocate 'co.aikar.commands', "shadow.${pluginName}.acf"
    relocate 'co.aikar.locales', "shadow.${pluginName}.locales"
    relocate 'de.exlll.configlib', "shadow.${pluginName}.configlib"
    relocate 'de.raidcraft.economy.wrapper', "de.raidcraft.skills.economy"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = true
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

tasks.withType(Javadoc) {
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

defaultTasks 'build'
